############## Standard Random-Effects NMA Model #################
######### With Uniform Prior for b/w Study Heterogeneity #########

model{

	### Loop through studies
	for(i in 1:ns){

		w[i,1] <- 0		# multi-arm adjustment; always 0 for study-specific baseline arm
		delta[i,t[i,1]] <- 0	# the random effect is 0 for study-specific baseline arms
		mu[i] ~ dt(0,1/(2.5)^2,7)	# weakly informative Student-t priors for each study-specific baseline (nuisance parameter)

	### for each study, loop through each arm
		for(k in 1:na[i]){
			r[i,k] ~ dbin(p[i,t[i,k]], n[i,k])			# binomial likelihood for events in each arm
			logit(p[i,t[i,k]]) <- mu[i] + delta[i,t[i,k]]
		}

	### loop through non-control arms for this study
		for(k in 2:na[i]){
			delta[i,t[i,k]] ~ dnorm(md[i,t[i,k]], taud[i,t[i,k]]) 		# random effects; trial-specific log-ORs
			md[i,t[i,k]] <- d[t[i,k]] - d[t[i,1]] + sw[i,k]	      		# md is a functional parameter; defined by basic parameters d which compare treatment k to A; 
			taud[i,t[i,k]] <- tau*2*(k-1)/k			      		# precision of LOR distribution; equal to tau when there are only 2 arms (homogeneous variance assumption)
			w[i,k] <- (delta[i,t[i,k]] - d[t[i,k]] + d[t[i,1]])     	# adjustment, multi-arm RCTs
	     		sw[i,k] <- sum(w[i, 1:(k-1)])/(k-1)				# equal to 0 when only 2 arms
		}
	}

	d[1] <- 0                         	# treatment effect is zero for reference treatment

	for(k in 2:nt){
	    d[k] ~ dt(0,1/(2.5)^2,7)		# weakly informative Student-t priors for the pooled treatment effects
	}

	sd ~ dlnorm(-2.70,1/1.52)          	# vague prior for between-trial SD (sd of the random effects). 
	tau <- 1/pow(sd,2)                	# between-trial precision 

	### pairwise ORs: returns OR of k vs. network reference (placebo)
	for(k in 1:nt){
	        log(or[1,k]) <- d[k]
	}
}