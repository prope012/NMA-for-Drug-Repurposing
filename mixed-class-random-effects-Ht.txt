############## Mixed "Fixed-Effects" NMA Model for Two Indications #################

model{

	### Loop through studies
	for(i in 1:ns){
		
		w[i,1] <- 0					# multi-arm adjustment; always 0 for study-specific baseline arm
		mu[i] ~ dt(0,1/(2.5)^2,7)			# weakly informative Student-t priors for each study-specific baseline (nuisance parameter)
		delta[i,t[i,1]] <- 0				# the random effect is 0 for study-specific baseline arms
	
	### for each study, loop through each arm
		for(k in 1:na[i]){
			r[i,k] ~ dbin(p[i,t[i,k]], n[i,k])				# binomial likelihood for events in each arm
			logit(p[i,t[i,k]]) <- mu[i] + delta[i,t[i,k]]			# mean now dependent on indication
		}

	### loop through non-control arms for this study
		for(k in 2:na[i]){
			delta[i,t[i,k]] ~ dnorm(md[i,t[i,k]], taud[i,t[i,k]]) 	
			md[i,t[i,k]] <- d[t[i,k],indication[i]] - d[t[i,1],indication[i]] + sw[i,k]	# md is a functional parameter; defined by basic parameters d which compare treatment k to A; 
			taud[i,t[i,k]] <- tau[indication[i]]*2*(k-1)/k			      		# precision of LOR distribution; equal to tau when there are only 2 arms (homogeneous variance assumption)
			w[i,k] <- (delta[i,t[i,k]] - d[t[i,k],indication[i]] + d[t[i,1],indication[i]]) # adjustment, multi-arm RCTs
	     		sw[i,k] <- sum(w[i, 1:(k-1)])/(k-1)	 
		}
	}

	d[1,1] <- 0                         	# treatment effect is zero for reference treatment in both indications
	d[1,2] <- 0
	gamma[1] <- 0
	epsilon[1,1] <- 0
	epsilon[1,2] <- 0
	
	for(i in 1:2){
		for(k in 2:nt){
			d[k,i] <- (i-1.5)*beta2 + gamma[k] + epsilon[k,i]			# express basic pars in terms of mixed effects model
		}
	}

	### priors for fixed regression parameters
	beta0 ~ dt(0,1/(2.5)^2,7)
	beta1 ~ dt(0,1/(2.5)^2,7)
	beta2 ~ dt(0,1/(2.5)^2,7)

	### prior for random treatment effects
	for(k in 2:nt){
		gamma[k] ~ dnorm(beta0 + beta1*(class[k]-1.5),taug)
	}
	taug <- pow(sdg,-2)
	sdg ~ dnorm(0,1)T(0,)
	
	### prior for random error terms
	for(i in 1:2){
		for(k in 2:nt){
			epsilon[k,i] ~ dnorm(0,taue)
		}
	}
	taue <- pow(sde,-2)
	sde ~ dnorm(0,1)T(0,)

	### priors for between-trial SD (sd of the random effects)
	for(i in 1:2){
		sdd[i] ~ dt(0,1/(2.5)^2,7)T(0,)
		tau[i] <- 1/pow(sdd[i],2)
	}		

	### draw from the posterior predictive distribution for all treatments and indications
	for(k in 1:nt){
		for(i in 1:2){
			d.new[k,i] ~ dnorm(d[k,i],pow(sdd[i],-2))
		}
	}

}